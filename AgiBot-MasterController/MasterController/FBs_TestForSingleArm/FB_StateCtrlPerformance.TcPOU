<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_StateCtrlPerformance" Id="{ce0084ea-11c0-49f6-8318-5d3735e6e14b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_StateCtrlPerformance EXTENDS FB_MasterArmCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR CONSTANT
	//max number of data recording points
	//3deg/s(360dge)=120s
	//record cycle(0.001s)
	m_dataSum:INT:=13000;
END_VAR

VAR
	//arm id
	m_armId:INT;
	
	//test step
	m_testStep:INT:=-1;
	
	//start motion
	m_startMotion:BOOL;
	m_startMotionLast:BOOL;
	
	//test joint id
	m_jntId:INT;
	
	//trajectory id
	m_trajId:INT;
	
	//home position
	m_homeDHPos:Vec7d;
	m_startDHJntPos:Vec7d;
	m_endDHJntPos:Vec7d;
	
	//joint LimitBuffer
	m_limitBuffer:LREAL:=0.0*g_deg2Rad;
	
	//basic function
	m_basicFun:FB_BasicFun;
	
	//DH joint position
	m_initDHPos,m_startDHPos,m_endDHPos :Vec7d;
	m_targetDHPos :Vec7d;
	
	//startJntPos,endJntPos :Vec8d;
	m_initJntPos,m_startJntPos,m_endJntPos :Vec7d;
	
	//Trajectory planner
	m_Planner: FB_TrajMoveJ;
	
	//motion parameters
	m_maxjntVel,m_maxjntAcc,m_maxjntJerk:Vec7d;
	
	//time
	m_time:LREAL;
	
	//data sum
	m_dataRecordSum:INT;
	
	//motion status
	m_motionFinished:BOOL;
	
	//sampling interval
	m_samplingInterval:INT:=4;
	
	//discard time
	m_discardDuration:LREAL:=1;
	
	//Number of data records
	m_RecordNumD2,m_RecordNumD3,m_RecordNumD2Init:ARRAY[1..g_mJntNum] OF INT;
	
	//valid number
	m_validNum:ARRAY[1..g_mJntNum] OF INT;
	
	//joint gap
	m_jntGap,m_jntGapInit:ARRAY[1..g_mJntNum] OF LREAL;
	
	//active torque
	//m_activeTrq:ARRAY[1..g_mJntNum] OF LREAL;
	
	//The current values of the second and third trajectories
	m_cmdDHJntPosD2,m_jntPosD2,m_jntTrqD2,m_jntCurD2,m_jntCurD2_Init:ARRAY[1..g_mJntNum,1..gvl_const.g_recordDataNum] OF LREAL;
	m_cmdDHJntPosD3,m_jntPosD3,m_jntTrqD3,m_jntCurD3,m_jntCurD3_Init:ARRAY[1..g_mJntNum,1..gvl_const.g_recordDataNum] OF LREAL;
	m_jntTrqD3_Reverse,m_jntCurD3_Reverse:ARRAY[1..g_mJntNum,1..gvl_const.g_recordDataNum] OF LREAL;
	
	//friction record value
	m_CoulombFriction,m_CoulombFrictionInit:ARRAY[1..g_mJntNum,1..gvl_const.g_recordDataNum] OF LREAL;
	m_CoulombFrictionSum,m_CoulombFrictionSumInit:ARRAY[1..g_mJntNum] OF LREAL;
	m_CoulombFrictionAve,m_CoulombFrictionRatio:ARRAY[1..g_mJntNum] OF LREAL;
	
	//unbalanced moment
	m_unbalancedMoment:ARRAY[1..g_mJntNum,1..gvl_const.g_recordDataNum] OF LREAL;	
	m_unbalancedMax,m_unbalancedMaxInit:Vec7d;
	
	//counter
	m_counter:INT;
	m_pointNum:INT;
	m_pointNumLast:INT;
	
	//show the friction at these point
	m_jntTestPos,m_jntTestPntFriction,m_jntTestPntUnbalancedTrq,m_jntTestPntFrictionInit:ARRAY[1..7,1..3] OF LREAL;	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[



]]></ST>
    </Implementation>
    <Method Name="basicTestMotion" Id="{7713d872-0177-488d-adb7-be30c294a150}">
      <Declaration><![CDATA[METHOD basicTestMotion : BOOL
VAR_INPUT
	i_masterArm :FB_MasterArm;
END_VAR

VAR
	i,j,k:INT;
	id:INT;
	validNum:INT;	
	gap:LREAL;
	curDHJntPos:Vec7d;
	ratio:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
CASE m_testStep OF 
	0: //init the trajectory
		m_jntId:=1;
		m_trajId:=1;
		m_jntCurD2:=m_jntCurD2_Init;		m_jntTrqD2:=m_jntCurD2_Init;		m_jntPosD2:=m_jntCurD2_Init;
		m_jntCurD3:=m_jntCurD3_Init;		m_jntTrqD3:=m_jntCurD3_Init;		m_jntPosD3:=m_jntCurD3_Init;
		m_CoulombFriction:=m_CoulombFrictionInit;
		m_jntTestPntFriction:=m_jntTestPntFrictionInit; m_jntTestPntUnbalancedTrq:=m_jntTestPntFrictionInit;
		m_unbalancedMoment:=m_jntCurD2_Init;
		m_unbalancedMax:=m_unbalancedMaxInit;
		
		m_CoulombFrictionSum:=m_CoulombFrictionSumInit;
		m_jntGap:=m_jntGapInit;
		m_RecordNumD2:=m_RecordNumD2Init; m_RecordNumD3:=m_RecordNumD2Init;
		m_motionFinished:=FALSE;
		m_testStep:=10;
		
		
	10: //Plan the trajectory
		//current DH joint position
		m_basicFun.activeJnt2DH(i_activeJntPos:= i_masterArm.curJntPos, o_DHJntPos=> curDHJntPos);		
		m_startDHPos:=m_homeDHPos;
		m_endDHPos:=m_homeDHPos;
		
		IF (m_trajId=1) THEN
			m_startDHPos[m_jntId]:=curDHJntPos[m_jntId];
			m_endDHPos[m_jntId]:=m_startDHJntPos[m_jntId];
		ELSIF (m_trajId=2) THEN
			m_startDHPos[m_jntId]:=m_startDHJntPos[m_jntId];
			m_endDHPos[m_jntId]:=m_endDHJntPos[m_jntId];
		ELSIF (m_trajId=3) THEN
			m_startDHPos[m_jntId]:=m_endDHJntPos[m_jntId];
			m_endDHPos[m_jntId]:=m_startDHJntPos[m_jntId];
		ELSIF (m_trajId=4) THEN
			m_startDHPos[m_jntId]:=m_startDHJntPos[m_jntId];
			m_endDHPos[m_jntId]:=m_homeDHPos[m_jntId];
		END_IF
		
		m_Planner.init(i_startJntPos:= m_startDHPos, 	i_targetJntPos:= m_endDHPos, 	i_maxJntVel:= m_maxjntVel, 	i_maxJntAcc:= m_maxjntAcc, 	i_maxJntJerk:= m_maxjntJerk);
		IF (m_Planner.isGene=FALSE) THEN
			m_time:=0; m_testStep:=50;
		ELSE
			m_time:=0; m_testStep:=20;
			m_dataRecordSum:=0;
			m_counter:=0;
			m_pointNum:=0;
			m_pointNumLast:=0;
		END_IF
		
	
	20: //start the motion
		m_time:=m_time+g_armCtrlCycleTime;
		m_Planner.evaluate(t:= m_time, jntPos=> m_targetDHPos, jntVel=> m_cmdJntVel, jntAcc=> m_cmdJntAcc);
		m_basicFun.DH2activeJnt(i_DHJntPos:= m_targetDHPos, o_activeJntPos=> m_cmdJntPos);
		
		IF (m_time>m_Planner.duration) THEN
			m_testStep:=30;
		END_IF		
		
		//updata the value of counter
		m_counter:=m_counter+1;
		IF (m_counter>=4*20) THEN
			m_counter:=0;
			m_pointNum:=m_pointNum+1;
		END_IF
		
		//Discard the first and last paragraphs of data
		IF (m_time<m_discardDuration) OR (m_time>(m_Planner.duration-m_discardDuration)) THEN
			m_counter:=0;
			m_pointNum:=m_pointNumLast;
		END_IF
		m_pointNumLast:=m_pointNum;
		
		//current DH joint position
		m_basicFun.activeJnt2DH(i_activeJntPos:= i_masterArm.curJntPos, o_DHJntPos=> curDHJntPos);
		
		//record the data---Move in the positive direction
		IF (m_trajId=2) AND (m_counter=0) AND (m_pointNum<=gvl_const.g_recordDataNum) AND (m_pointNum>0) THEN
			m_RecordNumD2[m_jntId]:=m_pointNum;
			m_jntCurD2[m_jntId,m_pointNum]:=i_masterArm.filtJntCurrent[m_jntId]*GVL_ArmJointParameters.g_motorDir[m_armId,m_jntId];
			m_jntTrqD2[m_jntId,m_pointNum]:=i_masterArm.curJntTrq[m_jntId];
			m_jntPosD2[m_jntId,m_pointNum]:=curDHJntPos[m_jntId];
			m_cmdDHJntPosD2[m_jntId,m_pointNum]:=m_targetDHPos[m_jntId];
			
			gvl_armTest.g_cmdDHJntPosD2[m_armId,m_jntId,m_pointNum]:=m_cmdDHJntPosD2[m_jntId,m_pointNum];
			gvl_armTest.g_jntPosD2[m_armId,m_jntId,m_pointNum]:=m_jntPosD2[m_jntId,m_pointNum];
			gvl_armTest.g_jntTrqD2[m_armId,m_jntId,m_pointNum]:=m_jntTrqD2[m_jntId,m_pointNum];
		END_IF
		
		//record the data---Move in the negative direction
		IF (m_trajId=3) AND (m_counter=0) AND (m_pointNum<=gvl_const.g_recordDataNum) AND (m_pointNum>0) THEN
			m_RecordNumD3[m_jntId]:=m_pointNum;
			m_jntCurD3[m_jntId,m_pointNum]:=i_masterArm.filtJntCurrent[m_jntId]*GVL_ArmJointParameters.g_motorDir[m_armId,m_jntId];
			m_jntTrqD3[m_jntId,m_pointNum]:=i_masterArm.curJntTrq[m_jntId];
			m_jntPosD3[m_jntId,m_pointNum]:=curDHJntPos[m_jntId];
			m_cmdDHJntPosD3[m_jntId,m_pointNum]:=m_targetDHPos[m_jntId];
			
			gvl_armTest.g_cmdDHJntPosD3[m_armId,m_jntId,m_pointNum]:=m_cmdDHJntPosD3[m_jntId,m_pointNum];
			gvl_armTest.g_jntPosD3[m_armId,m_jntId,m_pointNum]:=m_jntPosD3[m_jntId,m_pointNum];
			gvl_armTest.g_jntTrqD3[m_armId,m_jntId,m_pointNum]:=m_jntTrqD3[m_jntId,m_pointNum];
		END_IF
		
		//update the joint gap
		FOR i:=1 TO g_mJntNum BY 1 DO
			gap:=ABS(i_masterArm.curJntPos[i]-i_masterArm.curLinkPos[i]);
			IF (gap>m_jntGap[i]) THEN
				m_jntGap[i]:=gap;
			END_IF
		END_FOR
		
		//output the joint gap
		gvl_armTest.g_jntGap[m_armId]:=m_jntGap;
		
		
	30: //Update joint and trajectory numbers
		m_trajId:=m_trajId+1;
		IF (m_trajId<=4) THEN			
			m_testStep:=10;
		ELSE
			m_jntId:=m_jntId+1;
			m_trajId:=1;
			IF (m_jntId<=7) THEN
				m_testStep:=10;
			ELSE
				m_testStep:=40;
			END_IF
		END_IF
		
	
	(*
	35: //confirm to next joint test
		IF (gvl_armTest.g_nextStep[m_armId]=TRUE) THEN
			gvl_armTest.g_nextStep[m_armId]:=FALSE;
			m_testStep:=10;
		END_IF
	*)
		
		
	40: //Calculate technical indicators
		FOR i:=1 TO g_mJntNum BY 1 DO
			IF (m_RecordNumD2[i]>m_RecordNumD3[i]) THEN
				validNum:=m_RecordNumD3[i];
			ELSE
				validNum:=m_RecordNumD2[i];
			END_IF
			m_validNum[i]:=validNum;
			
			//Sort the third test data in reverse order
			FOR j:=1 TO validNum BY 1 DO
				id:=m_RecordNumD3[i]-j+1;
				m_jntCurD3_Reverse[i,j]:=m_jntCurD3[i,id];
				m_jntTrqD3_Reverse[i,j]:=m_jntTrqD3[i,id];
			END_FOR
			
			//Calculate frictional force
			FOR j:=1 TO validNum BY 1 DO
				//for positive direciton:T2=G-F-----for nagative direction:T3=G+F,  so F=(T3-T2)/2---G=(T2+T3)/2
				m_CoulombFriction[i,j]:=(m_jntTrqD3_Reverse[i,j]-m_jntTrqD2[i,j])/2;
				m_CoulombFrictionSum[i]:=m_CoulombFrictionSum[i]+m_CoulombFriction[i,j];
				
				//joint 2/3/5 need to get specific point's friction
				IF (i=2) OR (i=3) OR (i=5) THEN					
					//every joint has 3 test point
					FOR k:=1 TO 3 BY 1 DO
						IF ABS(m_jntPosD2[i,j]-m_jntTestPos[i,k])<(0.2*g_deg2Rad) THEN
							m_jntTestPntFriction[i,k]:=m_CoulombFriction[i,j];							
						END_IF		
					END_FOR								
				END_IF				
			END_FOR		
			
			//calculate the average of friction and ratio
			m_CoulombFrictionRatio[i]:=0;
			m_CoulombFrictionAve[i]:=m_CoulombFrictionSum[i]/validNum;
			FOR j:=1 TO validNum BY 1 DO
				ratio:=m_CoulombFriction[i,j]/m_CoulombFrictionSum[i];
				IF (ratio>0) AND (ratio>m_CoulombFrictionRatio[i]) THEN
					m_CoulombFrictionRatio[i]:=ratio;
				END_IF
			END_FOR
			gvl_armTest.g_CoulombFrictionAve[m_armId,i]:=m_CoulombFrictionAve[i];
			gvl_armTest.g_CoulombFrictionRatio[m_armId,i]:=m_CoulombFrictionRatio[i];
			
			//joint unbalanced torque 
			FOR j:=1 TO validNum BY 1 DO
				m_unbalancedMoment[i,j]:=(m_jntTrqD3_Reverse[i,j]+m_jntTrqD2[i,j])/2;
				//maxmum value
				IF ABS(m_unbalancedMoment[i,j])>m_unbalancedMax[i] THEN
					m_unbalancedMax[i]:=ABS(m_unbalancedMoment[i,j]);
				END_IF
				
				//joint 2/3/5 need to get specific point's friction
				IF (i=2) OR (i=3) OR (i=5) THEN					
					//every joint has 3 test point
					FOR k:=1 TO 3 BY 1 DO
						IF ABS(m_jntPosD2[i,j]-m_jntTestPos[i,k])<(0.2*g_deg2Rad) THEN
							m_jntTestPntUnbalancedTrq[i,k]:=m_unbalancedMoment[i,j];							
						END_IF		
					END_FOR								
				END_IF
			END_FOR
			
			//the unbalanced joint torque
			FOR k:=1 TO 3 BY 1 DO
				gvl_armTest.g_jntTestPntFriction[m_armId,i,k]:=m_jntTestPntFriction[i,k];				
				gvl_armTest.g_jntTestPntUnbalancedTrq[m_armId,i,k]:=m_jntTestPntUnbalancedTrq[i,k];
			END_FOR
		END_FOR
		
		//output to global variables
		gvl_armTest.g_unbalancedMax[m_armId]:=m_unbalancedMax;		
		
		//next step
		m_testStep:=50;
		
		
	50:
		m_testStep:=-1;
		m_motionFinished:=TRUE;
		gvl_armTest.g_writeData[m_armId]:=TRUE; //output the data to txt file
		
		
	-1: //	
END_CASE


//output the test information
gvl_armTest.g_jntId[m_armId]:=m_jntId;
gvl_armTest.g_trajId[m_armId]:=m_trajId;
gvl_armTest.g_testStep[m_armId]:=m_testStep;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="calcCmdJntPos" Id="{766c3d6c-34c4-484b-a02f-f712b3546c63}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntPos : BOOL

VAR_IN_OUT CONSTANT
	i_slaveIdx :INT;
	i_shoulderPose :ST_Frame;
	i_masterArm :FB_MasterArm;
	i_slaveStatus :ST_SlaveStatus;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
//show the joint angle
gvl_armTest.g_armActJntPos[m_armId]:=i_masterArm.curJntPos;
m_basicFun.activeJnt2DH(i_activeJntPos:= i_masterArm.curJntPos, o_DHJntPos=> gvl_armTest.g_armDHJntPos[m_armId]);


//start motion flag
m_startMotion:=gvl_armTest.g_startMotion[m_armId];
IF (m_startMotionLast=FALSE) AND (m_startMotion=TRUE) THEN
	m_testStep:=0;
	m_motionFinished:=FALSE;
END_IF
m_startMotionLast:=m_startMotion;


//transform to arm test motion
IF (m_startMotion=TRUE) THEN
	basicTestMotion(i_masterArm);
END_IF


//return the motion status
IF (m_testStep=-1) AND (m_motionFinished=TRUE) THEN
	gvl_armTest.g_startMotion[m_armId]:=FALSE;		
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{cd11ac78-3d6e-4252-8193-b6434569048a}">
      <Declaration><![CDATA[// override this function in each exact controller
// NOTICE: set joint control mode here, and do some initialization if needed
METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_masterArm	:FB_MasterArm;
END_VAR
VAR_IN_OUT 
	r_masterArmCtrlCmd :ST_ArmCtrlCmds;
END_VAR
VAR
	i:INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Init(i_masterArm,r_masterArmCtrlCmd);

// update commands to arm
copyCmds(r_masterArmCtrlCmd);

//init basic parameters
m_armId:=i_masterArm.armIdx;

//joint home position---joint motion start position and end position
FOR i:=1 TO g_mJntNum BY 1 DO
	m_homeDHPos[i]:=gvl_const.g_jntDHInitPos[m_armId,i];
	m_startDHJntPos[i]:=gvl_const.g_minJntDHPos[m_armId,i]+m_limitBuffer;
	m_endDHJntPos[i]:=gvl_const.g_maxJntDHPos[m_armId,i]-m_limitBuffer;	
END_FOR

//Joint 7 has a wide range of motion and requires special treatment
m_startDHJntPos[7]:=-100*g_deg2Rad;
m_endDHJntPos[7]:=100*g_deg2Rad;

//special treatment for joint 2 and joint 3
m_startDHJntPos[2]:=-2.0944; m_endDHJntPos[2]:=-0.5253;
m_startDHJntPos[3]:=1.04894; m_endDHJntPos[3]:=2.26893;

//joint velocity and acceleration
FOR i:=1 TO g_mJntNum BY 1 DO
	m_maxjntVel[i]:=3.0*g_deg2rad;
END_FOR
vecMultiNum(m_maxjntVel,3,m_maxjntAcc);
vecMultiNum(m_maxjntAcc,3,m_maxjntJerk);

//update the friction test point---m_jntTestPos,m_jntTestFriction:ARRAY[1..7,1..3] OF LREAL;	
FOR i:=1 TO g_mJntNum BY 1 DO
	m_jntTestPos[i,1]:=m_homeDHPos[i];
	m_jntTestPos[i,2]:=m_homeDHPos[i]+30.0*g_deg2Rad;
	m_jntTestPos[i,3]:=m_homeDHPos[i]-30.0*g_deg2Rad;
END_FOR
m_jntTestPos[5,2]:=m_homeDHPos[5]+90.0*g_deg2Rad;  m_jntTestPos[5,3]:=m_homeDHPos[5]-90.0*g_deg2Rad; 

]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_StateCtrlPerformance">
      <LineId Id="10" Count="3" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_StateCtrlPerformance.basicTestMotion">
      <LineId Id="9" Count="1" />
      <LineId Id="144" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="146" Count="0" />
      <LineId Id="187" Count="1" />
      <LineId Id="329" Count="0" />
      <LineId Id="363" Count="0" />
      <LineId Id="361" Count="0" />
      <LineId Id="364" Count="0" />
      <LineId Id="495" Count="1" />
      <LineId Id="151" Count="0" />
      <LineId Id="395" Count="0" />
      <LineId Id="256" Count="1" />
      <LineId Id="269" Count="0" />
      <LineId Id="145" Count="0" />
      <LineId Id="284" Count="0" />
      <LineId Id="437" Count="0" />
      <LineId Id="441" Count="0" />
      <LineId Id="438" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="445" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="439" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="134" Count="0" />
      <LineId Id="442" Count="0" />
      <LineId Id="135" Count="1" />
      <LineId Id="443" Count="0" />
      <LineId Id="137" Count="1" />
      <LineId Id="444" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="29" Count="5" />
      <LineId Id="169" Count="0" />
      <LineId Id="185" Count="0" />
      <LineId Id="175" Count="0" />
      <LineId Id="493" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="35" Count="1" />
      <LineId Id="38" Count="0" />
      <LineId Id="42" Count="1" />
      <LineId Id="287" Count="0" />
      <LineId Id="286" Count="0" />
      <LineId Id="44" Count="1" />
      <LineId Id="41" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="176" Count="0" />
      <LineId Id="178" Count="2" />
      <LineId Id="186" Count="0" />
      <LineId Id="181" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="487" Count="1" />
      <LineId Id="490" Count="0" />
      <LineId Id="494" Count="0" />
      <LineId Id="491" Count="1" />
      <LineId Id="489" Count="0" />
      <LineId Id="386" Count="0" />
      <LineId Id="388" Count="0" />
      <LineId Id="387" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="182" Count="0" />
      <LineId Id="164" Count="0" />
      <LineId Id="213" Count="0" />
      <LineId Id="217" Count="0" />
      <LineId Id="407" Count="0" />
      <LineId Id="446" Count="0" />
      <LineId Id="448" Count="0" />
      <LineId Id="408" Count="1" />
      <LineId Id="165" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="69" Count="1" />
      <LineId Id="166" Count="0" />
      <LineId Id="183" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="218" Count="0" />
      <LineId Id="410" Count="0" />
      <LineId Id="447" Count="0" />
      <LineId Id="449" Count="0" />
      <LineId Id="411" Count="1" />
      <LineId Id="167" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="219" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="229" Count="0" />
      <LineId Id="223" Count="0" />
      <LineId Id="226" Count="1" />
      <LineId Id="224" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="318" Count="0" />
      <LineId Id="320" Count="0" />
      <LineId Id="319" Count="0" />
      <LineId Id="168" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="141" Count="0" />
      <LineId Id="161" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="171" Count="0" />
      <LineId Id="157" Count="2" />
      <LineId Id="162" Count="1" />
      <LineId Id="160" Count="0" />
      <LineId Id="143" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="249" Count="0" />
      <LineId Id="270" Count="0" />
      <LineId Id="250" Count="1" />
      <LineId Id="253" Count="0" />
      <LineId Id="255" Count="0" />
      <LineId Id="254" Count="0" />
      <LineId Id="252" Count="0" />
      <LineId Id="475" Count="0" />
      <LineId Id="124" Count="2" />
      <LineId Id="189" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="193" Count="1" />
      <LineId Id="192" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="202" Count="0" />
      <LineId Id="205" Count="3" />
      <LineId Id="215" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="211" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="321" Count="0" />
      <LineId Id="373" Count="0" />
      <LineId Id="323" Count="0" />
      <LineId Id="325" Count="0" />
      <LineId Id="497" Count="0" />
      <LineId Id="356" Count="0" />
      <LineId Id="326" Count="0" />
      <LineId Id="355" Count="0" />
      <LineId Id="346" Count="0" />
      <LineId Id="351" Count="1" />
      <LineId Id="349" Count="1" />
      <LineId Id="328" Count="0" />
      <LineId Id="324" Count="0" />
      <LineId Id="353" Count="0" />
      <LineId Id="498" Count="0" />
      <LineId Id="507" Count="0" />
      <LineId Id="499" Count="0" />
      <LineId Id="502" Count="0" />
      <LineId Id="506" Count="0" />
      <LineId Id="503" Count="0" />
      <LineId Id="508" Count="1" />
      <LineId Id="504" Count="0" />
      <LineId Id="500" Count="0" />
      <LineId Id="512" Count="0" />
      <LineId Id="511" Count="0" />
      <LineId Id="357" Count="2" />
      <LineId Id="372" Count="0" />
      <LineId Id="365" Count="1" />
      <LineId Id="368" Count="0" />
      <LineId Id="461" Count="0" />
      <LineId Id="464" Count="7" />
      <LineId Id="462" Count="0" />
      <LineId Id="360" Count="0" />
      <LineId Id="389" Count="1" />
      <LineId Id="392" Count="1" />
      <LineId Id="472" Count="0" />
      <LineId Id="391" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="380" Count="0" />
      <LineId Id="394" Count="0" />
      <LineId Id="376" Count="0" />
      <LineId Id="510" Count="0" />
      <LineId Id="302" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="195" Count="0" />
      <LineId Id="118" Count="1" />
      <LineId Id="121" Count="1" />
      <LineId Id="425" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="247" Count="0" />
      <LineId Id="246" Count="0" />
      <LineId Id="241" Count="2" />
      <LineId Id="248" Count="0" />
      <LineId Id="315" Count="0" />
    </LineIds>
    <LineIds Name="FB_StateCtrlPerformance.calcCmdJntPos">
      <LineId Id="250" Count="0" />
      <LineId Id="256" Count="0" />
      <LineId Id="258" Count="0" />
      <LineId Id="295" Count="0" />
      <LineId Id="279" Count="0" />
      <LineId Id="271" Count="1" />
      <LineId Id="275" Count="1" />
      <LineId Id="315" Count="1" />
      <LineId Id="278" Count="0" />
      <LineId Id="274" Count="0" />
      <LineId Id="273" Count="0" />
      <LineId Id="252" Count="1" />
      <LineId Id="268" Count="2" />
      <LineId Id="305" Count="0" />
      <LineId Id="254" Count="0" />
      <LineId Id="217" Count="0" />
      <LineId Id="280" Count="2" />
      <LineId Id="284" Count="0" />
      <LineId Id="283" Count="0" />
    </LineIds>
    <LineIds Name="FB_StateCtrlPerformance.init">
      <LineId Id="12" Count="0" />
      <LineId Id="145" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="147" Count="0" />
      <LineId Id="151" Count="0" />
      <LineId Id="165" Count="0" />
      <LineId Id="168" Count="28" />
      <LineId Id="166" Count="0" />
      <LineId Id="146" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>